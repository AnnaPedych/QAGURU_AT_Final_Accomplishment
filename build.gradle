plugins {
    id 'java-library'
    id 'application'
    id 'io.qameta.allure' version '2.8.1'
}

repositories {
    maven {url = "https://dl.bintray.com/qameta/maven-unstable/"}
    mavenCentral()
}

def allureVersion = "2.13.8",
    selenideVersion = "5.20.0",
    junitVersion = "5.7.1"

allure {
    version = "${allureVersion}"
    autoconfigure = true
    aspectjweaver = true
    useJUnit5 {
        version = "${allureVersion}"
    }
}

dependencies {
    testImplementation(
            "org.aspectj:aspectjweaver:1.9.6",
            "io.rest-assured:rest-assured:4.3.1",
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-java-commons:$allureVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "org.hamcrest:hamcrest-all:1.3",
            "org.aeonbits.owner:owner:1.0.12",
            "ch.qos.logback:logback-classic:1.2.3",
            "io.qameta.allure:allure-ee-junit-platform:3.28.2",
            "org.junit.jupiter:junit-jupiter-api:$junitVersion",
            "org.junit.jupiter:junit-jupiter-params:$junitVersion",
            "com.googlecode.json-simple:json-simple:1.1",
            "io.rest-assured:json-schema-validator:4.3.3")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion", "org.slf4j:slf4j-simple:1.7.30")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties += System.properties
    System.setProperty("chromeoptions.prefs", "intl.accept_languages=en")
    System.setProperty("junit.jupiter.extensions.autodetection.enabled", "true")

    if (System.getProperty("threads") != null) {
        systemProperties += [
                'junit.jupiter.execution.parallel.enabled'                 : true,
                'junit.jupiter.execution.parallel.mode.default'            : 'concurrent',
                'junit.jupiter.execution.parallel.mode.classes.default'    : 'concurrent',
                'junit.jupiter.execution.parallel.config.strategy'         : 'fixed',
                'junit.jupiter.execution.parallel.config.fixed.parallelism': System.getProperty("threads").toInteger()
        ]
    }

    testLogging {
        lifecycle {
            events "started", "failed"
//            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}
test {
    useJUnitPlatform()
    systemProperties(System.getProperties())
}